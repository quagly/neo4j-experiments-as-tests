apply plugin: 'base'
apply plugin: 'groovy'

def neo4jVer  = '2.0.0-M06'
def groovyVer = '2.1.8'
def spockVer = '0.7-groovy-2.0'

defaultTasks 'test'

repositories {
  // added jcenter 
  jcenter()
  mavenCentral()
}

dependencies {
  compile "org.codehaus.groovy:groovy-all:${groovyVer}"
  compile "org.neo4j:neo4j:${neo4jVer}" // don't need all of neo4j, and only testing now anyway

  testCompile "org.spockframework:spock-core:${spockVer}" 
  testCompile "org.neo4j:neo4j-kernel:${neo4jVer}:tests"
}

// say why if test fails
// so that I don't have to open the html report to look
test {
  testLogging {
    // show STDOUT
    showStandardStreams = true
    // show what failed
    exceptionFormat = 'full'
  }
}

task createGraphGists ( dependsOn: 'classes', type: JavaExec ) {
  description = 'parses spock tests and generates GraphGist adoc files'
  main 'createGraphGists'
  classpath = sourceSets.main.runtimeClasspath
  ext.srcDir = new File( projectDir, 'src/test/groovy' )
  ext.destDir = new File( projectDir, 'out/graphGists' )
  inputs.dir  srcDir 
  outputs.dir destDir
  destDir.mkdirs()
}
